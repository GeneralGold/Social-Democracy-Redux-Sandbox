title: Investigating Domestic Enemies
on-arrival: month_actions += 1
view-if: spd_in_government = 1 and interior_minister_party = "SPD"
#card-image: img/portraits/SeveringCarl.jpg
#is-card: true
#new-page: true

# this is not a card...

= Investigating Domestic Enemies

As we have control over the Interior Ministry, we can direct police and intelligence services to investigate internal enemies. We can proscribe various organizations.

- @deport_hitler
- @ban_sa: Ban the SA.
- @unban_sa: Unban the SA.
- @persecute_sa
- @ban_sh: Ban the Stahlhelm.
- @unban_sh: Unban the Stahlhelm.
- @persecute_sh
- @ban_rfb: Ban the Communist RFB.
- @unban_rfb: Unban the RFB.
- @persecute_rfb
- @investigate_coup: Investigate the rumors of a coup plot.
- @root: Take no action for now.

@ban_sa
view-if: nazi_urgency >= 3 and sa_banned == 0
on-arrival: sa_strength *= 0.85; sa_strength *= 0.7 if interior_police_loyalty >= 0.7; sa_banned = 1; dvp_relation -= 3; prussian_police_loyalty -= 0.05; interior_police_loyalty -= 0.05; sa_militancy -= 0.05; sa_ban_timer = 6; coup_progress += 1; coup_progress += 1 if sh_banned; coup_progress += 1 if coup_progress >= 6;

As it turns out, the Nazi SA has many allies in the police and bureaucracy. Our relationship with the police weakens, and the SA can still organize illegally, right under the police's noses. [? if interior_police_loyalty >= 0.7 : Nevertheless, the SA is substantially weakend by our ban. ?]

@unban_sa
view-if: sa_banned = 1
on-arrival: sa_banned = 0; sa_strength += 100; sa_militancy += 0.05

The SA is no longer banned. They immediately go on a recruiting spree.

@persecute_sa
title: Further persecute the SA.
view-if: sa_banned = 1
choose-if: judicial_reform >= 3 and interior_police_loyalty >= 0.7
unavailable-subtitle: [? if judicial_reform < 3 : The judiciary would not allow this. ?] [? if interior_police_loyalty < 0.7 : The police are too disloyal to be trusted with this. ?]
on-arrival: sa_strength *= 0.7; sa_militancy -= 0.05

We have arrested more of the SA's leaders and confiscated their weapons.

@ban_sh
on-arrival: sh_strength *= 0.85; sh_strength *= 0.7 if interior_police_loyalty >= 0.7; sh_banned = 1; dvp_relation -= 5; prussian_police_loyalty -= 0.1; interior_police_loyalty -= 0.05; coalition_dissent += 1 if spd_in_government = 1 and in_grand_coalition = 1 and president = "Hindenburg"; sh_ban_timer = 6; coup_progress += 1; coup_progress += 1 if sa_banned; coup_progress += 1 if coup_progress >= 6;
view-if: sh_banned == 0

[? if president == "Hindenburg": Banning the Stahlhelm weakens our relationship with President Hindenburg, as well as the political right and the police. ?][? if president != "Hindenburg": Banning the Stahlhelm weakens our relationship with the political right and the police. ?] [? if spd_in_government = 1 and in_grand_coalition = 1 and president = "Hindenburg" : It also weakens our coalition due to the influence of Hindenburg on the right wing of our coalition. ?] In addition, the Stahlhelm still operates out of public sight.

@unban_sh
view-if: sh_banned = 1
on-arrival: sh_banned = 0; sh_strength += 100

The Stahlhelm is no longer banned.

@persecute_sh
title: Further persecute the Stahlhelm.
view-if: sh_banned = 1
choose-if: judicial_reform >= 4 and interior_police_loyalty >= 0.7
unavailable-subtitle: [? if judicial_reform < 4 : The judiciary would not allow this. ?] [? if interior_police_loyalty < 0.7 : The police are too disloyal to be trusted with this. ?]
on-arrival: sh_strength *= 0.7; sh_militancy -= 0.05; dvp_relation -= 5; prussian_police_loyalty -= 0.1; interior_police_loyalty -= 0.05; coalition_dissent += 1 if spd_in_government = 1 and in_grand_coalition = 1 and president = "Hindenburg";

We have arrested more of the Stahlhelm's leaders and confiscated their weapons.


@ban_rfb
view-if: communist_coalition < 3 and rfb_banned = 0
on-arrival: kpd_relation -= 15; rfb_strength -= 50; rfb_banned = 1; communist_coalition -= 1; z_relation += 5; dvp_relation += 5; prussian_police_loyalty += 0.1; interior_police_loyalty += 0.1; kpd_coalition_dissent += 2; coalition_dissent -= 1 if coalition_dissent > 0 and n_rfb_banned = 0; n_rfb_banned += 1

The ban is enthusiastically carried out by the police, perhaps too enthusiastically. The Communists take this as one more reason why the Socialists are traitors to the left. However, the moderate and center-right parties approve, as do the police.

@unban_rfb
view-if: rfb_banned = 1
on-arrival: rfb_banned = 0; kpd_relation += 5; rfb_strength += 50; prussian_police_loyalty -= 0.05; interior_police_loyalty -= 0.05

The Communist RFB is no longer banned.

@investigate_coup
on-arrival: sa_legal = 1 if sa_banned == 0; sa_legal = 0 if sa_banned == 1; sh_legal = 1 if sh_banned == 0; sh_legal = 0 if sh_banned == 1
view-if: coup_progress > 3
subtitle: We must verify whether or not the rumors are true and if they are take action to defend the republic.
go-to: investigate_success if interior_police_loyalty >= (0.1 + (coup_progress/10)); investigate_failure if interior_police_loyalty < (0.1 + (coup_progress/10))

@investigate_success
on-arrival: coup_info += 1

We have found evidence of seditious plots in the military and Harzburg Front, confirming our existing suspicions that such a plot was underway. Now we must decide what to do with this information.

- @act_immediately: We should act on this new information immediately.
- @wait_info: We need to compile more evidence so that we can truly foil this plot. Let's keep this information for later.
- @purge: We will use all of our evidence gathered to crush the coup plot!

@act_immediately
view-if: coup_info < 3
choose-if: judicial_reform >= 2
unavailable-subtitle: The judiciary is unconvinced and unwilling to allow such action until further information is gathered.
on-arrival: coup_info -= 1; coup_progress -= 1; coup_progress -= 1 if reichswehr_minister_party == "SPD"; workers_nsdap -= 2; new_middle_nsdap -= 4; old_middle_nsdap -= 4; rural_nsdap -= 2; pro_republic += 2; interior_police_loyalty -= 0.05

We arrest those implicated and take further efforts to foil these reactionary schemes, setting back the plot somewhat. Unfortunately, this has the effect of revealing to the coup plotters that we know of their plot, so they manage to cover their tracks somewhat and make further investigation harder.

@wait_info
view-if: coup_info < 3

We must play the long game so that we can find not only the participants of this plot but also the leadership and just how far it stretches. We shelve our current info as evidence and work to compile more by investigating associates and acquiantances of the plotters we find.

@purge
view-if: coup_info >= 3
subtitle: If the Harzburg Front hasn't been weakened enough, this may spur them into immediate action.
go-to: purge_success if ((sa_strength*sa_militancy*sa_legal)+(sh_strength*sh_militancy*sh_legal)*(1-interior_police_loyalty)) <= ((prussian_police_loyalty*prussian_police_strength*spd_prussia)+(rb_strength*rb_militancy*(1-dissent))); purge_failure if ((sa_strength*sa_militancy*sa_legal)+(sh_strength*sh_militancy*sh_legal)*(1-interior_police_loyalty)) > ((prussian_police_loyalty*prussian_police_strength*spd_prussia)+(rb_strength*rb_militancy*(1-dissent)))

@purge_success
on-arrival: coup_progress -= 5 if coup_progress >= 5; coup_progress = 0 if coup_progress < 5; workers_nsdap -= 5; new_middle_nsdap -= 10; old_middle_nsdap -= 10; rural_nsdap -= 2; pro_republic += 5; strife += 1; interior_police_loyalty -= 0.1

Having finally gathered enough evidence to truly paralyze the coup, we publicize our findings and launch sweeping arrests of the plotters involved. Reichswehr officers implicated are quickly discharged and placed under arrest or, for the more problematic ones, reassigned to entirely different posts to prevent them from enacting their plans. With many of their top schemers, plotters and leaders arrested over night, the remaining coup plot crumbles and the Harzburg Front is left grasping at straws as they consider but decide against an immediate uprising, certain that they would lose now with their plan shattered.  

German democracy is saved, for now.

@purge_failure
on-arrival: strife += 2; reichswehr_loyalty += 0.05; workers_nsdap -= 3; new_middle_nsdap -= 6; old_middle_nsdap -= 6; harzburg_purge = 1

Having finally gathered enough evidence to truly paralyze the coup, we publicize our findings and launch sweeping arrests of the plotters involved. Reichswehr officers implicated are quickly discharged and placed under arrest or, for the more problematic ones, reassigned to entirely different posts to prevent them from enacting their plans. With many of their top schemers, plotters and leaders arrested over night, the Harzburg Front is left scattered, but nevertheless, fearing that if they do not act now they would be completely crushed, have launched an uprising in their weakened position. SA forces storm barracks with the Stalhelm, either deposing or convincing the officers and seizing control over those Reichswehr forces. City halls are seized, their republican mayors ousted, while reactionary city governments deploy police forces on the side of the Nazi coup to suppress any potential resistance.

- @civil_war: It is a civil war, then.

@investigate_failure
on-arrival: coup_progress += 1; interior_police_loyalty -= 0.05; coalition_dissent += 1

Unfortunately, our investigations fail to turn up anything. Rumors are that the coup plotters were tipped off by infiltrators in the police, which, if true, would not only mean that our efforts were wasted but would also likely mean the coup plotters have accelerated their plans. Our coalition partners are annoyed meanwhile, believing our suspicions to be false and criticizing us for our paranoia.

@persecute_rfb
title: Further persecute the RFB.
view-if: rfb_banned = 1
on-arrival: rfb_strength *= 0.7; rfb_militancy -= 0.05; kpd_relation -= 10; kpd_coalition_dissent += 1; z_relation += 5; dvp_relation += 5; prussian_police_loyalty += 0.1; interior_police_loyalty += 0.1; coalition_dissent -= 1 if coalition_dissent > 0 and n_rfb_persecuted = 0; n_rfb_persecuted += 1

We have arrested more of the RFB's leaders and confiscated their weapons.
