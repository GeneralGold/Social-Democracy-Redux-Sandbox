title: KPD policy
subtitle: The KPD ministers are carrying out their policy portfolios.
new-page: true
view-if: (in_popular_front or in_left_front) and (labor_minister_party = "KPD" or economic_minister_party = "KPD" or finance_minister_party = "KPD" or interior_minister_party = "KPD" or foreign_minister_party = "KPD") and (kpd_policy_timer = 0)
on-arrival: kpd_policy_timer = 5
tags: event
max-choices: 1

# disable this event for now - the KPD is abstentionist in government. This position is loosely based on Paul Levi's ideas during the Kapp Putsch. A KPD that is willing to support an SPD government would necessarily have some different policy ideas from the Thälmann Stalinist line, so I looked to the earlier Paul Levi KPD as well as the French popular front for ideas.

# ironically the hypothetical KPD position towards the Popular Front is basically analogous to the SPD position towards the Brüning government (i.e. toleration).

# Sources: Gaido, Daniel. “Paul Levi and the Origins of the United-Front Policy in the Communist International.” Historical Materialism 25, no. 1 (2017): 131–74. https://doi.org/10.1163/1569206x-12341515.
# LaPorte, Norman H. “The German Communist Party in Saxony, 1924-1933 : Factionalism, Fratricide and Political Failure,” 1998. http://dspace.stir.ac.uk/handle/1893/3259.


= KPD Policy

The Communist-run ministries are pursuing their own policy course. It is up to us whether to support or oppose these policies.

# different policies for every minister choice

- @labor
- @economy
- @finance
- @foreign
- @interior
- @judicial

@labor
title: Labor Ministry
subtitle: The Labor Ministry is giving significant concessions to organized labor.
view-if: labor_minister_party = "KPD"

The KPD-controlled Labor Ministry is taking a policy of supporting labor in every dispute between workers and employees, leading to a proliferation of strikes across the country. Given the economic circumstances, there are worries that this could harm any potential economic recovery, not to mention the conflicts this may engender with the bourgeois parties.

Do we support their radical labor policy?

- @labor_support: Of course we support labor.
- @labor_oppose: No, this is too radical.

@labor_support
on-arrival: capital_strike_progress += 2; workers_kpd += 5; workers_spd += 3*(1-dissent); kpd_labor_support += 1; coalition_dissent += 1; z_relation -= 6; unemployment += 2; kpd_influence += 2

The pro-labor policy is popular among workers and is bringing the KPD more support, but in response businesses are laying off more workers and reducing hours. This is setting the government up for a confrontation with the capitalists.

@labor_oppose
on-arrival: kpd_coalition_dissent += 1; workers_spd -= 5; workers_kpd += 5; kpd_relation -= 6

We oppose the KPD's policy, and prevent it from being enacted. The KPD is displeased, as are the workers who would have stood to benefit.

@economy
title: Economy Ministry
subtitle: The Economy Ministry is pursuing a policy of nationalizations.
view-if: economic_minister_party = "KPD" and nationalization_progress < 5

The KPD-controlled Economy Ministry is planning a policy of mass nationalizations. [? if budget < 3 : They plan to use deficit spending to finance state takeovers of underutilized industrial enterprises, and run them for the benefit of the workers. ?] [? if budget >= 3 : They plan to buy out closed and underutilized industrial enterprises, and run them at full capacity for the benefit of the workers. ?] 

Do we support this policy?

- @economy_support: Yes, support the nationalization policy.
- @economy_oppose: No, this must be stopped.

@economy_support
on-arrival: nationalization_progress += 1; capital_strike_progress += 4; unemployed -= 2.5 if unemployed >= 6; unemployed -= 1.2 if works_councils >= 3; budget -= 3; works_program += 1; workers_spd += 4*(1-dissent); workers_kpd += 8*(1-dissent); unemployed_kpd += 4*(1-dissent); coalition_dissent += 2; dvp_relation -= 6; z_relation -= 4; new_middle_spd -= 3; old_middle_spd -= 3; economy_goal_completed += 1; economic_growth += 1.7 if economic_growth < 5; economic_growth += 0.3 if budget < 0; kpd_influence += 2

The KPD has gained credit with the workers for enabling many to keep their jobs. Meanwhile, the bourgeois parties and middle-class voters blame the SPD for enabling the nationalizations.

@economy_oppose
on-arrival: kpd_coalition_dissent += 1; workers_spd -= 5; workers_kpd += 5; kpd_relation -= 6

After opposing the nationalization policy, the KPD has commenced a propaganda campaign against us, accusing the SPD of yet another perfidy.

@finance
title: Finance Ministry
subtitle: The Finance Ministry is increasing taxes on the rich.
view-if: finance_minister_party = "KPD" and upper_tax_rates < 5

The KPD-controlled Finance Ministry is planning a policy of heavy tax hikes on the rich to fund their other programs and plans. [? if capital_strike_progress >= 6 and not capital_strike_seen : Supporting this policy could lead to a confrontation with the already upset capitalists. ?]

- @finance_support: Yes, we support increasing taxes on the rich.
- @finance_oppose: No, now is not the right time.

@finance_support
on-arrival: upper_tax_rates += 1; capital_strike_progress += 1; capital_strike_progress += 1 if upper_tax_rates > 1; capital_strike_progress += 1 if upper_tax_rates > 3; workers_spd += 2*(1-dissent); workers_kpd += 3*(1-dissent); old_middle_spd -= 4; new_middle_spd -= 3; coalition_dissent += 1; coalition_dissent += 1 if capital_strike_progress >= 5; budget += 2; dvp_relation -= 3; unemployed += 1; finance_goal_completed += 1; inflation -= 0.6; economic_growth -= 0.4; kpd_influence += 2

We support the financial policy of the KPD-controlled Finance Ministry, which is popular amongst the workers and helps raise [? if budget <= 2 : much needed ?] funds for our other programs. Meanwhile, the bourgeois parties blame us for allowing these tax hikes to take place.

@finance_oppose
on-arrival: kpd_coalition_dissent += 1; workers_spd -= 3; workers_kpd += 3; kpd_relation -= 5; workers_spd -= 2 if budget < 0; workers_kpd += 2 if budget < 0

We oppose the KPD's planned tax hikes on the rich, prompting them to launch a propaganda campaign against us. [? if budget < 0 : This propaganda campaign is bolstered by the fact that we are currently running a deficit. ?]

@foreign
title: Foreign Ministry
subtitle: The Foreign Ministry is aiding the Soviet Union.
view-if: foreign_minister_party = "KPD" and soviet_relation < 10

The KPD-controlled Foreign Ministry is planning to send economic aid to the Soviet Union.

- @foreign_support: Yes, we support aiding the Soviet Union.
- @foreign_oppose: No, this must be stopped.

@foreign_support
on-arrival: budget -= 1; soviet_relation += 2; coalition_dissent += 1; foreign_goal_completed += 1; kpd_influence += 2; rfb_militancy += 0.04; rfb_strength += 40; reformist_dissent += 5

We support the KPD's sending of aid to the Soviet Union, and allow it to take place. Some on the right of the party suggest that the KPD are using the Foreign Ministry to plot with the Soviet Union against the Republic, but we dismiss these concerns.

@foreign_oppose
on-arrival: kpd_coalition_dissent += 1; kpd_relation -= 3; kpd_relation -= 2 if kpd_party_leader != "Conciliators"; soviet_relation -= 1

We block the planned sending of aid to the Soviet Union, disappointing the Communists, as well as the Soviets themselves who had looked forward to the aid.

@interior
title: Interior Ministry
subtitle: The Interior Ministry is engaged in a purge of the security services.
view-if: interior_minister_party = "KPD"

The KPD-controlled Interior Ministry has recently begun a purge of its security services, removing suspected fascists from servive and replacing them with communists and, less frequently, social democrats. They justify this as necessary to combat the reactionary threat to the republic.

- @interior_support: Yes, we support purging the security services.
- @interior_oppose: No, this seems to be a pretext for a KPD power grab.

@interior_support
on-arrival: interior_police_loyalty += 0.1; kpd_influence += 2; new_middle_kpd += 3; new_middle_nsdap -= 2; new_middle_dnvp -= 2; pro_republic += 3; reformist_dissent += 5; unemployed += 0.3; rfb_strength += 20; sa_strength -= 10; sh_strength -= 10

We allow the KPD to proceed with the purges, significantly weakening the sway of reactionaries over our police forces. At the same time, however, Communist forces are bolstered. The right of our party warns that the police is just being turned from one anti-democratic force into another.

@interior_oppose
on-arrival: kpd_coalition_dissent += 1; kpd_relation -= 5; workers_spd -= 2; workers_kpd += 2; interior_police_loyalty -= 0.05;

The purges are ended at our opposition, disappointing the Communists, who are quick to campaign on our refusal. Meanwhile, anticommunist sentiment in the police is strengthened by the attempted but blocked purge, and grows.

@judicial
title: Justice Ministry
subtitle: The Justice Ministry has launched a sweeping reform of the judiciary.
view-if: justice_minister_party = "KPD" and judicial_reform < 5

The KPD-controlled Justice Ministry has begun sacking reactionary judges and replacing them with socialist or even outright communist ones as part of a scheme to end the reactionary bias in the country. Some in our party fear that the reactionary bias is just being replaced by a communist one, however.

- @judicial_support: Yes, we support purging the security services.
- @judicial_oppose: No, this seems to be a pretext for a KPD power grab.

@judicial_support
on-arrival: budget -= 1; kpd_influence += 2; judicial_reform += 2; new_middle_kpd += 2; coalition_dissent += 1; coup_progress += 1

We allow the KPD to continue with their reforms. The courts have become much less reactionary, though at the same time noticeably more communist.

@judicial_oppose
on-arrival: kpd_coalition_dissent += 1; kpd_relation -= 2

We oppose the reforming of the courts in the Communists' image. They are upset by this decision, as the courts continue to rule against them.